apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.14.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0-rc01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.leanback:leanback:1.0.0'
    implementation 'com.google.android.material:material:1.2.0-alpha02'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation project(':interaction')
    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') { transitive = true }
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
    implementation "com.squareup.okhttp3:okhttp:4.1.0"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha03'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha03'
}

if (!project.hasProperty("upload_keystore_file")) {
    ext.upload_keystore_file = "keystore.jks"
}
if (!project.hasProperty("upload_keystore_secret")) {
    ext.upload_keystore_secret = "not a secret"
}
if (!project.hasProperty("upload_index_rebellion_alias")) {
    ext.upload_index_rebellion_alias = "upload-indexrebellion"
}
if (!project.hasProperty("upload_index_rebellion_secret")) {
    ext.upload_index_rebellion_secret = "not a secret"
}

fabric_key = project.property("fabric_key")

android {
    compileSdkVersion 29
    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "com.rubyhuntersky.quizmaker"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 8
        versionName "1.0.$versionCode"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [fabric_io_id: fabric_key]
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
    }
    signingConfigs {
        release {
            storeFile file(upload_keystore_file)
            storePassword upload_keystore_secret
            keyAlias upload_index_rebellion_alias
            keyPassword upload_index_rebellion_secret
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

